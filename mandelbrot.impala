extern "C" {
    fn print_header(int, int) -> ();
    fn put_i8(i8) -> ();
}

fn range(a: int, b: int, body: fn(int, fn())) -> () {
    if a < b {
        body(a);
        range(a+1, b, body, return)
    }
}

fn main() -> int {
    let w = 16000; // fixed command line input
    let h = 16000; // fixed command line input
    let mut bit_num = 0;
    
    let mut byte_acc : i8 = (0 as i8);
    let iter = 50;
    let limit: f64 = 2.0;
    
    let mut Zr: f64;
    let mut Zi: f64;
    let mut Cr: f64;
    let mut Ci: f64;
    let mut Tr: f64;
    let mut Ti: f64;

    print_header(w, h);

    for y in range(0, h) {
        for x in range(0, w) {
            Zr = 0.0;
            Zi = 0.0;
            Tr = 0.0;
            Ti = 0.0;
            Cr = (2.0*(x as f64)/(w as f64) - 1.5);
            Ci = (2.0*(y as f64)/(h as f64) - 1.0);
        
            for i in range(0, iter) {
                if Tr+Ti > limit*limit {
                    break()
                }
                Zi = 2.0*Zr*Zi + Ci;
                Zr = Tr - Ti + Cr;
                Tr = Zr * Zr;
                Ti = Zi * Zi;
            }
       
            byte_acc <<= 1 as i8; 
            if (Tr+Ti <= limit*limit) {
                byte_acc |= 0x01 as i8;
            }
            
            ++bit_num; 

            if bit_num == 8 {
                put_i8(byte_acc);
                byte_acc = 0 as i8;
                bit_num = 0;
            } else if x == w-1 {
                put_i8(byte_acc << ((8-w%8) as i8));
                byte_acc = 0 as i8;
                bit_num = 0;
            }
        }
    }
    0
}
